(do
    (def add (lambda (a b) (+ a b)))
    (assert! (== (add 2 3) 5) "lambda should bind arguments and return the result of the body")

    (def multi-expr (lambda (x)
        (* x 3)
        (+ x 5)
    ))
    (assert! (== (multi-expr 3) 8) "lambda should evaluate body expressions sequentially and return the last result")

    (def make-adder (lambda (x)
        (lambda (y) (+ x y))
    ))
    (def add-10 (make-adder 10))
    (assert! (== (add-10 5) 15) "lambda should capture and retain variables from its lexical scope (closure)")

    (def nums (list 1 2 3 4 5))
    (def double (lambda (x) (* x 2)))
    (assert! (== (map nums double) (list 2 4 6 8 10)) "map should apply the lambda to each item and return a new list")
    (assert! (== (map () double) ()) "map on an empty list should return an empty list")

    (def is-even (lambda (x) (== (% x 2) 0)))
    (assert! (== (filter nums is-even) (list 2 4)) "filter should return only items that satisfy the truthy condition")
    (assert! (== (filter () is-even) ()) "filter on an empty list should return an empty list")

    (def sum (lambda (acc x) (+ acc x)))
    (assert! (== (reduce nums 0 sum) 15) "reduce should accumulate the list into a single value")
    (assert! (== (reduce () 100 sum) 100) "reduce on an empty list should return the initial accumulator")

    (def args (list 1 2 3))
    (assert! (== (apply (lambda (a b c) (* a b c)) args) 6) "apply should work with lambdas and a list of arguments")

    (assert! (any? (list false false true false) (lambda (x) x)) "any? should return true if at least one element satisfies the predicate")
    (assert! (not (any? (list false false false) (lambda (x) x))) "any? should return false if no elements satisfy the predicate")
    (assert! (not (any? () (lambda (x) x))) "any? on an empty list should return false")

    (assert! (all? (list true true true) (lambda (x) x)) "all? should return true if all elements satisfy the predicate")
    (assert! (not (all? (list true false true) (lambda (x) x))) "all? should return false if any element fails the predicate")
    (assert! (all? () (lambda (x) x)) "all? on an empty list should return true")    

    (def unsorted-nums (list 5 2 4 1 3))
    (assert! (== (sort unsorted-nums) (list 1 2 3 4 5)) "sort without a callable should sort elements in ascending order")

    (def desc-comparator (lambda (a b) (> a b)))
    (assert! (== (sort unsorted-nums desc-comparator) (list 5 4 3 2 1)) "sort with a callable should order elements based on the comparator")
    (assert! (== (sort ()) ()) "sort on an empty list should return an empty list")
)
